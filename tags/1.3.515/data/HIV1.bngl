# BNG version of the Phillip Acute HIV infection model*
#
# * A. N. Phillips, SCIENCE, 271:497-499 (1996).

begin model

begin parameters
  gamma 1.36 
  tau	0.2
  mu	1.36e-3
  beta	2.7e-4
  p  	0.1
  alpha 3.6e-2
  sigma 2
  delta 0.33
  pi    100

  CD4_init  gamma*tau/mu
  HIV_init  100
end parameters

begin molecule types
  CD4(HIV~0~L~A)
  HIV()
  I()
  Null()
end molecule types

begin seed species
  CD4(HIV~0)  CD4_init
  HIV()       HIV_init
  I()         1
  Null()      0
end  seed species

begin observables
  Molecules V HIV()
end observables

begin reaction rules

Production of activated CD4: \
  I() -> I() + CD4(HIV~0) gamma*tau

CD4 natural death: \
  CD4() -> Null() mu

Latent infection: \
  CD4(HIV~0) + HIV() -> CD4(HIV~L) + HIV() (1-p)*beta

Active infection: \
  CD4(HIV~0) + HIV() -> CD4(HIV~A) + HIV() p*beta

Activation of infected cells: \
  CD4(HIV~L) -> CD4(HIV~A) alpha

Virus production: \
  CD4(HIV~A) -> CD4(HIV~A) + HIV() pi

Virus removal: \
  HIV() -> Null() sigma

Actively infected CD4 death: \
  CD4(HIV~A) -> Null() (delta-mu)

end reaction rules

end model

#ACTIONS
generate_network();	
writeMfile();
simulate_ode({t_end=>500,n_steps=>100,rtol=>1e-12,atol=>1e-12});

