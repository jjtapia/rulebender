#NAME: S_p_mixed.txt

begin parameters


RAS_post_eq    1

RAS0 1         # Signal max at 1
S0   100000    # Initial scaffold concentration
RAF0 100000    # Initial RAF concentration
MEK0 100000    # Initial MEK concentration
ERK0 500000    # Initial ERK concentration

#GENERIC PARAMETERS

joint     1e-6   #BASE: 1e-6

a1 joint  #BASE: 1e-6  -  Association of MEK, unphosphorylated ERK, and phosphorylated RAF with scaffold 
d1 0.1    #BASE: 0.1   -  Dissociation of MEK, unphosphorylated ERK, and phosphorylated RAF from scaffold
d2 1000   #BASE: 1000  -  Dissociation of phosphorylated ERK and unphosphorylated RAF from scaffold
p1 joint  #BASE: 1e-6  -  Phosphorylation of RAF by RAS, MEK by RAF, and ERK by MEK in cytoplasm
p2 100    #BASE: 100   -  Phosphorylation of MEK by RAF, and ERK by MEK on scaffold
u1 0.1    #BASE: 0.1   -  Dephosphorylation of RAF, MEK, and ERK
 

#SCAFFOLD ASSOCIATION-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

kf_uRAF_S 0         #base: 0 
kr_uRAF_S d2        #base: 100  
kf_pRAF_S a1        #base: 2e-6
kr_pRAF_S d1        #base: 0.1

kf_uMEK_S a1        #base: 2e-6
kr_uMEK_S d1        #base: 0.1
kf_pMEK_S a1        #base: 2e-6
kr_pMEK_S d1        #base: 0.1

kf_uERK_S a1        #base: 2e-6
kr_uERK_S d1        #base: 0.1
kf_pERK_S 0         #base: 0
kr_pERK_S d2        #base: 10

#----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

#ACTIVATION

k_cyt_RAS_RAF p1*1e6 #base: 1e-6 #rescaled from 1e-6 so the maximal signal is 1
k_cyt_RAF_MEK p1     #base: 1e-6
k_cyt_MEK_ERK p1     #base: 1e-6

k_S_RAF_MEK p2       #base: 100 
k_S_MEK_ERK p2       #base: 100

#DEACTIVATION

d_RAF u1           #base: 0.1
d_MEK u1           #base: 0.1
d_ERK u1           #base: 0.1

d_RAF_S u1         #base: 0.1
d_MEK_S u1         #base: 0.1
d_ERK_S u1         #base: 0.1

#NEGATIVE FEEDBACK

d_neg_f           0.1

end parameters

begin molecule types

 ERK_c22(S1~Y~Yp~Ypp)
 ERK_f22(s,S1~Y~Yp~Ypp)
 ERK_s22(s,S1~Y~Yp~Ypp)
 MEK_c22(S1~Y~Yp~Ypp)
 MEK_f22(s,S1~Y~Yp~Ypp)
 MEK_s22(s,S1~Y~Yp~Ypp)
 RAF_c22(S1~U~P)
 RAF_f22(s,S1~U~P)
 RAF_s22(s,S1~U~P)
 RAS_c22(GDP~U~P)
 RAS_f22(GDP~U~P)
 RAS_s22(GDP~U~P)
 S_f22(d,raf,mek,erk)
 S_s22(raf,mek,erk)

end molecule types

begin species

#######################################################################
RAF_c22(S1~U) 		                              RAF0
MEK_c22(S1~Y)                                         MEK0
ERK_c22(S1~Y)                                         ERK0
RAS_c22(GDP~P)                                        RAS0
########################################################################
S_s22(raf,mek,erk)                                     S0
MEK_s22(s,S1~Y)                                        MEK0
RAF_s22(s,S1~U) 		                       RAF0
ERK_s22(s,S1~Y)                                        ERK0
RAS_s22(GDP~P)                                         RAS0
######################################################################
S_f22(d,raf,mek,erk)                                     S0
RAF_f22(s,S1~U) 	 	                         RAF0
MEK_f22(s,S1~Y)                                          MEK0
ERK_f22(s,S1~Y)                                          ERK0
RAS_f22(GDP~P)                                           RAS0
######################################################################




end species

begin reaction rules


#########################################################
#COMB-22#######################################################################################################
#########################################################

	#CYTOSOL

	# ACTIVATION IN CYTOSOL

	RAF_f22(s,S1~U) + RAS_f22(GDP~P) -> RAF_f22(s,S1~P) + RAS_f22(GDP~P)    k_cyt_RAS_RAF

	MEK_f22(s,S1~Y) + RAF_f22(s,S1~P)   -> MEK_f22(s,S1~Yp) + RAF_f22(s,S1~P)  2*k_cyt_RAF_MEK
        MEK_f22(s,S1~Yp) +  RAF_f22(s,S1~P) -> MEK_f22(s,S1~Ypp) + RAF_f22(s,S1~P)  k_cyt_RAF_MEK

	ERK_f22(s,S1~Y)  + MEK_f22(s,S1~Ypp) -> ERK_f22(s,S1~Yp)  + MEK_f22(s,S1~Ypp)   2*k_cyt_MEK_ERK
        ERK_f22(s,S1~Yp) + MEK_f22(s,S1~Ypp) -> ERK_f22(s,S1~Ypp) + MEK_f22(s,S1~Ypp)  k_cyt_MEK_ERK

	# Binding to scaffold

	RAF_f22(s,S1~U) + S_f22(raf)  <-> RAF_f22(s!1,S1~U).S_f22(raf!1)        kf_uRAF_S ,kr_uRAF_S
	RAF_f22(s,S1~P) + S_f22(raf)  <-> RAF_f22(s!1,S1~P).S_f22(raf!1)        kf_pRAF_S ,kr_pRAF_S  
 
	MEK_f22(s,S1~Y) + S_f22(mek)   <-> MEK_f22(s!1,S1~Y).S_f22(mek!1)       kf_uMEK_S ,kr_uMEK_S
	MEK_f22(s,S1~Yp) + S_f22(mek)  <-> MEK_f22(s!1,S1~Yp).S_f22(mek!1)      kf_uMEK_S ,kr_uMEK_S
	MEK_f22(s,S1~Ypp) + S_f22(mek) <-> MEK_f22(s!1,S1~Ypp).S_f22(mek!1)     kf_pMEK_S ,kr_pMEK_S

	ERK_f22(s,S1~Y) + S_f22(erk) <-> ERK_f22(s!1,S1~Y).S_f22(erk!1)        kf_uERK_S, kr_uERK_S
	ERK_f22(s,S1~Yp) + S_f22(erk) <-> ERK_f22(s!1,S1~Yp).S_f22(erk!1)      kf_uERK_S, kr_uERK_S
	ERK_f22(s,S1~Ypp) + S_f22(erk) <-> ERK_f22(s!1,S1~Ypp).S_f22(erk!1)    kf_pERK_S, kr_pERK_S

	# MEK phosphorylation by RAF MEK on scaffold  

	RAF_f22(s!1,S1~P).S_f22(raf!1,mek!2).MEK_f22(s!2,S1~Y)  -> RAF_f22(s!1,S1~P).S_f22(raf!1,mek!2).MEK_f22(s!2,S1~Yp)   2*k_S_RAF_MEK
	RAF_f22(s!1,S1~P).S_f22(raf!1,mek!2).MEK_f22(s!2,S1~Yp) -> RAF_f22(s!1,S1~P).S_f22(raf!1,mek!2).MEK_f22(s!2,S1~Ypp)   k_S_RAF_MEK

	# ERK phosphorylation by MEK on scaffold

	ERK_f22(s!1,S1~Y).S_f22(erk!1,mek!2).MEK_f22(s!2,S1~Ypp)  ->  ERK_f22(s!1,S1~Yp).S_f22(erk!1,mek!2).MEK_f22(s!2,S1~Ypp)    2*k_S_MEK_ERK
        ERK_f22(s!1,S1~Yp).S_f22(erk!1,mek!2).MEK_f22(s!2,S1~Ypp)  ->  ERK_f22(s!1,S1~Ypp).S_f22(erk!1,mek!2).MEK_f22(s!2,S1~Ypp)   k_S_MEK_ERK  


	# DEACTIVATION 

        RAF_f22(S1~P) -> RAF_f22(S1~U) d_RAF
        MEK_f22(S1~Ypp) -> MEK_f22(S1~Yp) 2*d_MEK
        MEK_f22(S1~Yp)  -> MEK_f22(S1~Y)  d_MEK

	ERK_f22(S1~Ypp) -> ERK_f22(S1~Yp) 2*d_ERK
	ERK_f22(S1~Yp)  -> ERK_f22(S1~Y)  d_ERK

######################
#CYT-22########################################################
######################

	# ACTIVATION IN CYTOSOL

	RAF_c22(S1~U) + RAS_c22(GDP~P) -> RAF_c22(S1~P) + RAS_c22(GDP~P)    k_cyt_RAS_RAF

        RAF_c22(S1~P) + MEK_c22(S1~Y)  -> RAF_c22(S1~P) + MEK_c22(S1~Yp)   2*k_cyt_RAF_MEK
        RAF_c22(S1~P) + MEK_c22(S1~Yp) -> RAF_c22(S1~P) + MEK_c22(S1~Ypp) k_cyt_RAF_MEK


	MEK_c22(S1~Ypp) + ERK_c22(S1~Y) -> MEK_c22(S1~Ypp) + ERK_c22(S1~Yp)   2*k_cyt_MEK_ERK
	MEK_c22(S1~Ypp) + ERK_c22(S1~Yp) -> MEK_c22(S1~Ypp) + ERK_c22(S1~Ypp) k_cyt_MEK_ERK  

	# DEACTIVATION IN CYTOSOL

        RAF_c22(S1~P) -> RAF_c22(S1~U) d_RAF


	#DISTRIBUTIVE
 
	MEK_c22(S1~Ypp) -> MEK_c22(S1~Yp) 2*d_MEK
	MEK_c22(S1~Yp)  -> MEK_c22(S1~Y)  d_MEK

	ERK_c22(S1~Ypp) -> ERK_c22(S1~Yp) 2*d_ERK
	ERK_c22(S1~Yp)  -> ERK_c22(S1~Y)  d_ERK

#################
#SCAFF-22#############
#################

#	#ACTIVATION ON SCAFFOLD----------------------------------------------------------------------------------------------------------------------------------------

	# ACTIVATION IN CYTOSOL

	RAF_s22(s,S1~U) + RAS_s22(GDP~P) -> RAF_s22(s,S1~P) + RAS_s22(GDP~P)    k_cyt_RAS_RAF

	# Binding to scaffold

	RAF_s22(s,S1~U) + S_s22(raf)  <-> RAF_s22(s!1,S1~U).S_s22(raf!1)        kf_uRAF_S ,kr_uRAF_S
	RAF_s22(s,S1~P) + S_s22(raf)  <-> RAF_s22(s!1,S1~P).S_s22(raf!1)        kf_pRAF_S ,kr_pRAF_S  
  
	MEK_s22(s,S1~Y) + S_s22(mek) <-> MEK_s22(s!1,S1~Y).S_s22(mek!1)         kf_uMEK_S ,kr_uMEK_S
	MEK_s22(s,S1~Yp) + S_s22(mek) <-> MEK_s22(s!1,S1~Yp).S_s22(mek!1)       kf_uMEK_S ,kr_uMEK_S
	MEK_s22(s,S1~Ypp) + S_s22(mek) <-> MEK_s22(s!1,S1~Ypp).S_s22(mek!1)     kf_pMEK_S ,kr_pMEK_S

	ERK_s22(s,S1~Y) + S_s22(erk) <-> ERK_s22(s!1,S1~Y).S_s22(erk!1)        kf_uERK_S, kr_uERK_S
	ERK_s22(s,S1~Yp) + S_s22(erk) <-> ERK_s22(s!1,S1~Yp).S_s22(erk!1)      kf_uERK_S, kr_uERK_S
	ERK_s22(s,S1~Ypp) + S_s22(erk) <-> ERK_s22(s!1,S1~Ypp).S_s22(erk!1)    kf_pERK_S, kr_pERK_S

	# MEK phosphorylation by RAF MEK on scaffold  

	RAF_s22(s!1,S1~P).S_s22(raf!1,mek!2).MEK_s22(s!2,S1~Y) -> RAF_s22(s!1,S1~P).S_s22(raf!1,mek!2).MEK_s22(s!2,S1~Yp)   2*k_S_RAF_MEK
	RAF_s22(s!1,S1~P).S_s22(raf!1,mek!2).MEK_s22(s!2,S1~Yp) -> RAF_s22(s!1,S1~P).S_s22(raf!1,mek!2).MEK_s22(s!2,S1~Ypp)   k_S_RAF_MEK

	# ERK phosphorylation by MEK on scaffold

	ERK_s22(s!1,S1~Y).S_s22(erk!1,mek!2).MEK_s22(s!2,S1~Ypp)  -> ERK_s22(s!1,S1~Yp).S_s22(erk!1,mek!2).MEK_s22(s!2,S1~Ypp)    2*k_S_MEK_ERK
	ERK_s22(s!1,S1~Yp).S_s22(erk!1,mek!2).MEK_s22(s!2,S1~Ypp) -> ERK_s22(s!1,S1~Ypp).S_s22(erk!1,mek!2).MEK_s22(s!2,S1~Ypp)   k_S_MEK_ERK  

	#DEACTIVATION-------------------------------------------------------------------------------------------------------------------------------------

        RAF_s22(S1~P) -> RAF_s22(S1~U) d_RAF

	MEK_s22(S1~Ypp) -> MEK_s22(S1~Yp) 2*d_MEK
	MEK_s22(S1~Yp)  -> MEK_s22(S1~Y)  d_MEK

	ERK_s22(S1~Ypp) -> ERK_s22(S1~Yp) 2*d_ERK
	ERK_s22(S1~Yp)  -> ERK_s22(S1~Y)  d_ERK

end reaction rules

begin observables

c22_ERK_cyt_act      ERK_c22(S1~Ypp)    
s22_ERK_cyt_act      ERK_s22(s,S1~Ypp)
f22_ERK_cyt_act      ERK_f22(s,S1~Ypp)    

end observables

generate_network({overwrite=>1});
writeMfile();

simulate_ode({suffix=>equil,t_end=>100000,n_steps=>1000,sparse=>1,steady_state=>1});
simulate_ode({t_end=>100000,n_steps=>1000,steady_state=>1});
#setConcentration("RAS_f22(GDP~P)",RAS_post_eq);
#setConcentration("RAS_c22(GDP~P)",RAS_post_eq);
#setConcentration("RAS_s22(GDP~P)",RAS_post_eq);
#simulate_ode({t_end=>200, n_steps=>200});





